
;LAST UPDATE: 16.04.2010 savelij

DD		EQU 16				;ÑÄíÄ
MM		EQU 4				;åÖëüñ
YY		EQU 10				;ÉéÑ
DATA		EQU DD|MM<<5|YY<<9|#8000

		INCLUDE "ports_ngs.a80"
		INCLUDE "sdcomand.a80"

;ÄÑêÖë áÄÉêìáäà çÄâÑÖççéâ èêéòàÇäà
ADRLOAD		EQU #8000

RAMCOD		EQU #4080			;ÄÑêÖë êÄÅéíõ éëçéÇçéÉé äéÑÄ
RAM8KB		EQU #6000			;ÄÑêÖë ÅìîÖêÄ Ñãü èÖêÖÅêéëäà äéÑÄ
STRPAG		EQU #8000			;ÄÑêÖë äìÑÄ áÄÉêìáäà

		DI
		LD SP,RAMCOD
		XOR A
		OUT (VOL1),A
		OUT (VOL2),A
		OUT (VOL3),A
		OUT (VOL4),A
		OUT (VOL5),A
		OUT (VOL6),A
		OUT (VOL7),A
		OUT (VOL8),A		;áÄÉãìòàãà åéÑ èéêíõ Ñãü àëèéãúáéÇÄçàü ÅìîÖêÄ

;éÜàÑÄçàÖ áÄÉêìáäà èêéòàÇäà ëé ëèÖäÄ
		LD B,0			;óàíÄÖå 256 èéêí ëíÄíìëÄ
		IN A,(ZXSTAT)		;çÄ èêÖÑåÖí àáåÖçÖçàü ëéëíéüçàü 
		RRA
		JR C,RDBYT01
		DJNZ $-5
		DEC B			;ëóÖíóàä éÅçìãàãëü, ëéëíéüçàÖ èéêíÄ çÖ àáåÖçàãéëú
		JR RDBYT03		;Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì

RDBYT01		IN A,(ZXCMD)		;óàíÖå èêàòÖÑòàâ ÅÄâí àá èéêíÄ äéåÄçÑ
		LD C,A			;ëéïêÄçàãà Ñãü èêéÇÖêäà
		IN A,(ZXDATRD)		;óàíÖå ÅÄâí àá èéêíÄ ÑÄççõï
		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		CP C			;ëêÄÇçàÇÄÖå èêàòÖÑòàÖ ÅÄâíõ
		JR NZ,RDBYT03		;çÖ ëéÇèÄãà, Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì
		CP #55			;èêàòÖã ÅÄâí #55?
		JR NZ,RDBYT03		;Öëãà çÖí, Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì
		LD B,0			;óàíÄÖå 256 êÄá àáåÖçÖçàÖ èéêíÄ äéåÄçÑ
		IN A,(ZXSTAT)
		RRA
		JR C,RDBYT02
		DJNZ $-5
		DEC B
		JR RDBYT03		;çÖ ÑéÜÑÄãàëú, Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì

RDBYT02		IN A,(ZXCMD)		;áÄÅêÄãà ÅÄâí àá èéêíÄ äéåÄçÑ
		LD C,A			;ëéïêÄçàãà Ñãü èêéÇÖêäà
		IN A,(ZXDATRD)		;áÄÅêÄãà ÅÄâí àá èéêíÄ ÑÄççõï
		CP C			;ëêÄÇçàãà
		JR NZ,RDBYT03		;Öëãà çÖ ëéÇèÄÑÄûí, Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì
		CP #AA			;ùíé #AA?
RDBYT03		EXA			;ÇêÖåÖççé ëèêüíÄãà îãÄÉà èêéñÖëëéêÄ
		LD HL,GRUZILA
		LD DE,RAMCOD
		LD BC,RAMCEND-VERLOAD
		LDIR			;èÖêÖçÖëãà éëçéÇçéâ äéÑ Ç ÄÑêÖë êÄÅéíõ
		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		EXA			;ÇÖêçìãà ëèêüíÄççõÖ îãÄÉà
		JP Z,GRUZIM2		;Öëãà ÇëÖ ìëãéÇàü ëéÇèÄãà, áÄèìëäÄÖå áÄÉêìáóàä
		JP GRUZIM0		;àçÄóÖ Éêìáàå ëíÄçÑÄêíçìû èêéòàÇäì àá èáì

GRUZILA

		DISP RAMCOD

VERLOAD		DB "Loader"
		DW DATA

;èìíú Ñé îÄâãÄ èêéòàÇäà GS
F_PATH		DB "SYSTEMZX\NEOGS\NEOGS.ROM",0

TXT1		DB "not found",0
TXT1E
TXT2		DB "beta",0
TXT2E
TXT3		DB "stable",0
TXT3E

GRUZIM0		CALL RROMSD		;áÄÉêìÜÄÖå à áÄèìëäÄÖå èêéòàÇäì ë SD äÄêíõ, Öëãà éçÄ íÄå Öëíú
		JP GS105		;àçÄóÖ áÄÉêìÜÄÖå èêéòàÇäì àá èáì à áÄèìëäÄÖå

GRUZIM2		LD A,#11		;äéçîàÉìêàå çÉë
		OUT (GSCFG0),A		;éíêìÅÄÖå èáì à ÇêìÅÄÖå óÄëíéíì 12åÉñ
		XOR A
		OUT (MPAG),A		;ÇäãûóÄÖå ëíêÄçàñì 0
GRUZIM		IN A,(ZXSTAT)		;èéëíéüççé ÜÑÖå äéåÄçÑ éí ëèÖäÄ
		RRA
		JR NC,$-3
		IN A,(ZXCMD)		;èéâåÄãà äéåÄçÑì
		CP #1D			;ùíé äéåÄçÑÄ èêéÇÖêäà? 
		JR NZ,GRUZIM1
		LD A,#76
		OUT (ZXDATWR),A		;éíÑÄÖå ÅÄâí àÑÖçíàîàäÄñàà áÄÉêìáóàäÄ
		OUT (CLRCBIT),A
		JR GRUZIM		;à èêéÑéãÜÄÖå ÜÑÄíú äéåÄçÑ

GRUZIM1		CP #F3
		JP Z,GS105		;èêà èéëíìèãÖçàà äéåÄçÑ #F3 à
		CP #F4
		JP Z,GS105		;#F4 áÄÉêìÜÄÖå à áÄèìëäÄÖå èêéòàÇäì àá èáì
		CP LOW (FLOADE-FLOADER)/2+1
		JP NC,GS105		;ÄçÄãéÉàóçé çÖ äéåÄçÑÄ áÄÉêìáóàäÄ èÖêÖïéÑàå çÄ éëçéÇçéÖ èáì
		ADD A,A
		LD L,A
		LD H,0
		LD DE,GRUZIM2
		PUSH DE			;çÄ ëíÖäÖ ÄÑêÖë ÇéáÇêÄíÄ èéëãÖ àëèéãçÖçàü äéåÄçÑõ
		LD DE,FLOADER
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)		;áÄÅêÄãà ÄÑêÖë àëèéãçÖçàü äéåÄçÑõ
		EX DE,HL
		JP (HL)			;èéÖïÄãà çÄ àëèéãçÖçàÖ

FLOADER		DW LOADROM		;áÄÉêìáäÄ ROM ëé ëèÖäÄ
		DW JPLDROM		;áÄèìëä áÄÉêìÜÖççéÉé
		DW GS105		;áÄèìëíàíú áÄòàíéÖ Ç ROM
		DW RROMSD		;áÄèìëä ROM ë SD äÄêíõ
		DW LOADCOD		;áÄÉêìáäÄ äéÑÄ ëé ëèÖä
		DW LDINSD		;áÄÉêìáäÄ ë SD äÄêíõ
		DW RUNCOD		;áÄèìëä äéÑÄ
		DW STATSD		;ëíÄíìë áÄÉêìÜÖççéÉé
		DW VERPAGE		;ÇÖêëàü FPAG èêéòà
		DW GET_CRC		;èéãìóàíú CRC16
FLOADE

;èÖêÖÑÄóÄ çÄ ëèÖä ÇÖêëàà Ç TXT ÇàÑÖ
VERPAGE		IN A,(ZXDATRD)		;áÄÅêÄãà ÅÄâí çéåÖêÄ áÄèêéòÖççéâ ëíêÄçàñõ èáì
		AND 7			;ëíÄêòàÖ 5 Åàí Ç àÉçéêÖ
		EXA			;ÇêÖåÖççé ëèêüíÄãà îãÄÉà èêéñÄ
		LD A,#30
		OUT (GSCFG0),A		;Çäãûóàãà 10 åÉñ à ÇÖêçìãà èáì
		EXA			;ÇÖêçìãà îãÄÉà
		ADD A,A			;çÄå çìÜçÄ ÇíéêõÖ 32ä ëíêÄçàñõ èáì
		AND A			;ÇõÅêÄçÄ ëíêÄçàñÄ 0?
		JR NZ,VERPAG1		;Öëãà çÖí, íé àÑÖå çÄ àáÇãÖóÖçàÖ èéãëÖÑçàï 8 ÅÄâí
		LD HL,VERLOAD		;Ñãü áÄÉêìáóàäÄ ÅÖêÖå àá ÑêìÉéÉé ÄÑêÖëÄ
		LD DE,RAMCEND
		LD BC,8
		PUSH DE
		LDIR
		POP DE
		JR VERPAG2		;èéòãà çÄ èÖêÖÇéÑóàä Ç íÖäëíéÇõâ ÇàÑ

VERPAG1		INC A
		OUT (MPAG),A		;Çäãûóàãà èéëãÖÑçàà 16ä ÇõÅêÄççéâ ëíêÄçàñõ èáì
		LD HL,#FFF8
		LD BC,8
		LD DE,RAMCEND
		PUSH DE
		LDIR			;èÖêÖçÖëãà 8 ÅÄâí àá ïîéëíÄ èáì
		POP DE
VERPAG2		LD A,#11
		OUT (GSCFG0),A		;éíêìÅàãà èáì à ÇÖêçìãà 12åÉñ
		XOR A
		OUT (MPAG),A		;ÇÖêçìãà ëíêÄçàñì 0 éáì
		LD B,8			;èêéÇÖêüå èÖêÖçÖëÖççõÖ 8 ÅÄâí, Ä çÖ #FF ãà íÄå?
		LD A,(DE)
		INC DE
		INC A
		JR NZ,VFPGA1		;çÖ #FF, èêéÑéãÜÄÖå
		DJNZ $-5
		DEC SP			;íÄäà ÇëÖ 8 ÅÄâí #FF, ÇÖêëàà ì èáì ëíêÄçàñõ çÖíì
		DEC SP
		POP DE
		LD HL,TXT1
		LD BC,TXT1E-TXT1
		LDIR			;éíÑÄíú íÖäëí éÅ ùíéå
		JR VFPGA0

VFPGA1		DEC SP
		DEC SP
		POP DE
		CALL UNVERS		;êÄëèÄäéÇäÄ èéãìóÖççõï 8 ÅÄâí Ç íÖäëí
VFPGA0		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		LD HL,RAMCEND
		LD BC,ZXDATWR
1		LD A,(HL)
		OUTI
		EXA
		CALL WDN
		EXA
		AND A
		JR NZ,$-1B		;éíÑÄÖå íÖäëí ÇÖêëàà èéäÄ çÖ ÇëíêÖíàíëü ÅÄâí 0, äéíéêõâ íéÜÖ éíÑÄÖå
		RET

;êÄëèÄäéÇôàä ÑÄíõ
UNVERS		LD HL,6
		ADD HL,DE		;èêéèìëíàãà 6 ÅÄâí íÖäëíÄ ÇÖêëàà
		LD C,(HL)		;áÄÅêÄãà åãÄÑòàâ ÅÄâí ÇÖêëàà
		LD (HL),#20		;çÄ ÖÉé åÖëíé èêéÅÖã
		INC HL
		LD B,(HL)		;áÄÅêÄãà ëíÄêòàâ ÅÄâí
		LD A,C			;Çáüãà åãÄÑòàâ ÅÄâí ÇÖêëàà
		AND #1F			;çÄë àçíÖêÖëìûí åãÄÑòàÖ 5 Åàí (ÑÖçú åÖëüñÄ)
		JR NZ,$+4
		RES 7,B			;Öëãà èéãìóàãëü 0, ÇéáåéÜçé ùíé ÅÖíÄ ÇÖêëàü
		CP 32
		JR C,$+4		;Ç åÖëüñÖ çÖ åéÜÖí Åõíú ÅéãÖÖ 31 Ñçü
		RES 7,B			;àçÄóÖ ùíé ÅÖíÄ ÇÖêëàü
		CALL A2TXT		;èÖêÖÇéÑàå èéãìóÖççéÖ óàëãé Ç íÖäëí
		SRL B			;ëÑàçìãà ÇÖêëàû çÄ 1 Åàí ÇãÖÇé óíéÅõ çéåÖê åÖëüñÄ
		RR C			;éäÄáÄãëü Ç éÑçéå ÅÄâíÖ
		LD A,C			;áÄÅêÄãà ùíéí ÅÄâí
		RRCA
		RRCA
		RRCA
		RRCA			;ëÑÇàçìãà çìÜçõÖ Åàíõ Ç åãÄÑòàÖ êÄáêÄÑõ ÅÄâíÄ
		AND #0F			;éëíÄÇàãà çìÜçõÖ 4 ÅàíÄ (çéåÖê åÖëüñÄ)
		JR NZ,$+4		;çéåÖê åÖëüñÄ çÖåéÜÖí Åõíú 0
		RES 6,B			;àçÄóÖ ùíé ÅÖíÄ ÇÖêëàü
		CP 13			;à çéåÖê åÖëüñÄ çÖ åéÜÖí Åõíú ÅéãúòÖ 12
		JR C,$+4
		RES 6,B			;àçÄóÖ ÅÖíÄ ÇÖêëàü
		LD (HL),#2E		;éíÑÖãàãà çéåÖê Ñçü åÖëüñÄ íéóäéâ
		INC HL
		CALL A2TXT		;äéçÇÖêíàêéÇÄãà çéåÖê åÖëüñÄ
		LD (HL),#2E		;íÄä ÜÖ éíÑÖãàãà íéóäéâ
		INC HL
		LD A,B			;Çáüãà éëíÄÇòàÖëü Åàíõ
		AND #3F			;çÄå çìÜçõ 6 Åàí çéåÖêÄ ÉéÑÄ
		CALL A2TXT		;äéçÇÖêíçìãà Ç íÖäëí
		BIT 6,B			;ÅÖíÄ àãà õíÄÅã ÇÖêëàü?
		JR NZ,UNVERS1
		LD DE,TXT2		;ÇëÖ-íÄäà ÅÖíÄ, é óÖå à ÑéÅÄÇãüÖå íÖäëí ä ÇÖêëàà
		LD BC,TXT2E-TXT2
		JR UNVERS2

UNVERS1		LD DE,TXT3		;ÇÖêëàü ëíÄÅã
		LD BC,TXT3E-TXT3
UNVERS2		LD (HL),#20		;èÖêÖÑ íÖäëíéå (ÅÖíÄ àãà ëíÄÅã) ÇëíÄÇãüÖå èêéÅÖã
		INC HL
		EX DE,HL
		LDIR			;èÖêÖíÄôàãà íÖäëí
		EX DE,HL
		LD (HL),0		;áÄÇÖêòàãà ëíêéäì çìãÖå
		RET

;èÖêÖÇéÑ "A" Ç ÑÖëüíàóçõâ ÇàÑ à Ç íÖäëí. Ñãü óàëÖã éí 0 Ñé 99
A2TXT		PUSH HL			;ëéïêÄçàãà ÄÑêÖë äìÑÄ ãéÜàíú ëäéçÇÖêóÖççéÖ
		LD L,A
		LD H,0
		LD DE,10
		XOR A
		DEC A
		INC A
		SBC HL,DE
		JR NC,$-3		;ëóàíÄÖå äéãàóÖëíÇé ÑÖëüíäéÇ Ç óàëãÖ
		ADD HL,DE		;ÇÖêçìãà èÖêÖÅéê ÇõóàíÄçàü
		ADD A,"0"		;èÖêÖÇÖãà Ç íÖëäíéÇõâ ÇàÑ èéãìóÖççéÖ óàëãé
		LD D,A			;èéäÄ ëéïêÄçàãà
		LD A,L			;Çáüãà éëíÄÇòàÖëü ÖÑàçàñõ óàëãÄ
		ADD A,"0"		;äéçÇÖêíìãà Ç íÖäëí
		POP HL			;ÇÖêçìãà ÄÑêÖë äìÑÄ ãéÜàíú
		LD (HL),D		;èéãéÜàãà ÑÖëüíäà áÄÑÄççéÉé óàëãÄ
		INC HL
		LD (HL),A		;èéãéÜàãà ÖÑàçàñõ íéÉé ÜÖ óàëãÄ
		INC HL
		RET

;áÄÉêìáäÄ ë SD äÄêíõ èé ìäÄáÄççéåì èìíà
;èÖêÇõâ ÅÄâí-çéåÖê ëíêÄçàñõ éáì äìÑÄ çÄóàçÄíú Éêìáàíú 
;ÑÄãÖÖ ÅÄâíõ íÖäëíéÇÄü ëíêéäÄ èìíà à àåÖçà îÄâãÄ
;äéçÖñ ëíêéäà ÅÄâí 0, éç ÜÖ ëíéè ÅÄâí
LDINSD		LD BC,ZXDATRD		;ÄÑêÖë èéêíÄ ÑÄççõï
		LD HL,RAMCEND		;ÄÑêÖë äìÑÄ ëíêéäì èìíà ëäãÄÑàêéÇÄíú
		PUSH HL
		IN A,(C)		;èêàçüãà çéåÖê ëíêÄçàñõ éíäìÑÄ çÄóàçÄíú áÄÉêìáäì
		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		EXA
		CALL WDY
		IN A,(C)
		LD (HL),A
		INC HL
		AND A
		JR NZ,$-8		;èêàçàåÄÖå ëíêéäì èéäÄ çÖ ÇëíêÖíàëü ÅÄâí 0
		EXA
		POP HL
		JP LOAD_SD		;ÑÄãÖÖ áÄèìëäÄÖå áÄÉêìáäì ë SD äÄêíõ

;ëíÄíìë áÄÉêìáäà îÄâãÄ ë SD äÄêíõ
STATSD		LD A,(STATUS)
		OUT (ZXDATWR),A
		OUT (CLRCBIT),A
		RET

;áÄÉêìáäÄ à áÄèìëä èêéòàÇäà ë SD äÄêíõ
;àåü, èìíú à ÄÑêÖë îàäëàêéÇÄç
RROMSD		LD HL,F_PATH		;ÄÑêÖë ëíêéäà îàäëàêéÇÄççéÉé èìíà Ñãü áÄÉêìáäà 
		XOR A
		CALL LOAD_SD		;áÄÉêìÜÄÖå
		AND A
		RET NZ			;Öëãà éòàÅäÄ, íé Éêìáàå àá èáì
		JP JPLDROM

;áÄèìëä äéÑÄ Ç ãûÅéâ ëíêÄçàñÖ
;0-çéåÖê ëíêÄçàñõ éáì
;1-åãÄÑòàâ ÅÄâí ÄÑêÖëÄ áÄèìëäÄ
;2-ëíÄêòàâ ÅÄâí ÄÑêÖëÄ áÄèìëäÄ
RUNCOD		LD BC,ZXDATRD		;ÄÑêÖë èéêíÄ ÑÄççõï
		IN A,(C)		;èêàçüãà çéåÖê ë íêÄçàñõ éáì
		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		OUT (MPAG),A
		CALL WDY
		IN L,(C)		;èêàçüãà åãÄÑòàâ ÅÄâí ÄÑêÖëÄ áÄèìëäÄ
		CALL WDY
		IN H,(C)		;èêàçüãà ëíÄêòàâ ÅÄâí ÄÑêÖëÄ áÄèìëäÄ
		JP (HL)			;áÄèìëäÄÖå

;0-çéåÖê ëíêÄçàñõ éáì
;1-åãÄÑòàâ ÅÄâí Ñãàçõ áÄÉêìáäà
;2-ëíÄêòàâ ÅÄâí Ñãàçõ áÄÉêìáäà
;áÄÉêìáäÄ çÖ ÅéãÖÖ 32äÅ
LOADCOD		LD BC,ZXDATRD		;ÄÑêÖë èéêíÄ ÑÄççõï
		LD HL,#8000		;ÄÑêÖë çÄóÄãÄ áÉêìáäà
		IN A,(C)		;èêàçüãà çéåÖê ëíêÄçàñõ éáì
		OUT (CLRCBIT),A		;ëÅêéëàãà äéåÄçÑ Åàí
		OUT (MPAG),A		;Çäãûóàãà áÄÑÄççìû ëíêÄçàñì éáì
		CALL WDY
		IN E,(C)		;èêàçüãà åãÄÑòàâ ÅÄâí Ñãàçõ áÄÉêìáäà
		CALL WDY
		IN D,(C)		;èêàçüãà ëíÄêòàâ ÅÄâí Ñãàçõ áÉÄêìáäà
LOADCO1		CALL WDY
		INI
		LD A,H
		AND A
		RET Z			;Öëãà éáì äéçóàãéëú, ÇõïéÑàå
		DEC DE
		LD A,D
		OR E
		JR NZ,LOADCO1		;Éêìáàå ëäéäÄ ìäÄáÄçé
		RET

;áÄÉêìáäÄ èêéòàÇäà 32äÅ ëé ëèÖäÄ
LOADROM		XOR A
		OUT (MPAG),A		;ÇäãûóÄÖå ëíêÄçàñì
		LD HL,#8000		;ÄÑêÖë áÄÉêìáäà
		OUT (CLRCBIT),A		;ëÅêéë äéåÄçÑ ÅàíÄ
		LD BC,ZXDATRD		;ÄÑêÖë èéêíÄ ÑÄççõï
		CALL WDY
		INI
		LD A,H
		AND A
		JR NZ,$-7		;Éêìáàå èéäÄ èÄåüíú çÖ äéçóàíëü
		RET

;ÉêìáàãäÄ ëíÄçÑÄêíçéâ èêéòàÇäà àá ROM
GS105		LD HL,STRPAG		;ÄÑêÖë äìÑÄ èÖêÖçéëàíú
		LD A,4			;èÖêÖçéëàíú 4 äìëäÄ èé 8 äàãéÅÄâí
MOV1		EXA			;èêüóÖå ëóÖíóàä
		LD A,3
		OUT (GSCFG0),A		;Çäãûóàãà èáì
		LD A,2
		OUT (MPAG),A		;ëíêÄçàñì 2 ëé ëíÄçÑÄêíçéâ èêéòàÇäéâ 
		PUSH HL
		LD DE,RAM8KB
		LD BC,#2000
		LDIR			;èÖêÖçÖëãà Ç ÅìîÖê 8 äàãéÅÄâí
		LD A,#31
		OUT (GSCFG0),A		;êíäãûóàãà èáì
		XOR A
		OUT (MPAG),A		;Çäãûóàãà ëíêÄçàñì 0 éáì
		POP DE
		LD HL,RAM8KB
		LD BC,#2000
		LDIR			;èÖêÖçÖëãà àá ÅìîÖêÄ 8 äàãéÅÄâí
		EX DE,HL
		EXA
		DEC A
		JR NZ,MOV1		;à íÄä 4 êÄáÄ

;áÄèìëä áÄÉêìÜÖççéâ èêéòàÇäà
JPLDROM		XOR A
		OUT (MPAG),A		;Çäãûóàãà ëíêÄçàñì 0 éáì
		LD A,#13
		OUT (GSCFG0),A		;Çäãûóàãà óÄëíéíì 12åÉñ, éíäãûóàãà èáì à áÄôàíàãà éáì éí áÄèàëà
		JP 0			;ëíÄêíìÖå ëíÄçÑÄêíçìû èêéòàÇäì

;ÜÑÖå èéäÄ ëèÖä ÑÄëí ÅÄâí
WDY		IN A,(ZXSTAT)
		RLA
		JR NC,WDY
		RET

;ÜÑÖå èéäÄ ëèÖä áÄÅÖêÖí ÅÄâí àá èéêíÄ
WDN		IN A,(ZXSTAT)
		RLA
		JR C,WDN
		RET

;èéÑëóÖí CRC16
GET_CRC		LD A,2
		OUT (MPAG),A
		DEC A
		OUT (GSCFG0),A
		IN A,(ZXDATRD)
		CP #80
		JR C,SCHET
		LD HL,(#8000)
		LD DE,8
		AND A
		SBC HL,DE
		SRL H
		RR L
		SRL H
		RR L
		SRL H
		RR L
		SRL H
		RR L
		JR ERR_OK

SCHET		AND 7
		LD HY,A
		LD IX,#8000
		LD C,(IX)
		LD B,(IX+1)
		CALL CRC16
		LD C,(IX)
		LD B,(IX+1)
		LD A,#81
		AND A
		SBC HL,BC
		JR NZ,OUT_ERR		;CRC16 áÄÉéãéÇäÄ ERROR
		LD A,HY
		ADD A,A
		ADD A,A
		ADD A,A
		ADD A,A
		LD E,A
		LD D,0
		LD IX,#8000+8
		ADD IX,DE		;Ç IX ÄÑêÖë ÇõÅêÄççéÉé ÅãéäÄ
		LD C,(IX+4)
		LD B,(IX+5)		;Ç BC ÑãàçÄ ÅãéäÄ
		LD E,(IX+1)
		LD L,(IX+2)
		LD H,(IX+3)
		LD A,L
		AND #7F
		LD D,A			;Ç DE ëåÖôÖçàÖ Ç ëíêÄçàñÖ
		ADD HL,HL
		LD A,2
		ADD A,H
		OUT (MPAG),A		;çéåÖê ëíêÄçàñõ
		PUSH IX
		LD IX,#8000
		ADD IX,DE		;Ç IX ÄÑêÖë çÄóÄãÄ ÅãéäÄ
		CALL CRC16
		POP IX
		LD C,(IX+6)
		LD B,(IX+7)
		LD A,#82
		AND A
		SBC HL,BC
		JR NZ,OUT_ERR
		ADD HL,BC
ERR_OK		LD A,#80

;CRC16 ERROR
;#80-CRC16 ÅãéäÄ OK
;#81-CRC16 áÄÉéãéÇäÄ ERROR
;#82-CRC16 ÅãéäÄ ERROR
OUT_ERR		OUT (CLRCBIT),A
		OUT (ZXDATWR),A
		CALL WDN
		LD A,L
		OUT (ZXDATWR),A
		CALL WDN
		LD A,H
		OUT (ZXDATWR),A
		RET

CRC16		LD HL,#FFFF
		LD DE,#1021
CRC_0		LD A,(IX)
		INC IX
		EXA
		LD A,LX
		OR HX
		JR NZ,CRC_3
		INC LY
		LD A,LY
		OUT (MPAG),A
		LD IX,#8000
CRC_3		EXA
		XOR H
		LD H,A
		LD A,8
CRC_1		ADD HL,HL
		JR NC,CRC_2
		EXA
		LD A,L
		XOR E
		LD L,A
		LD A,H
		XOR D
		LD H,A
		EXA
CRC_2		DEC A
		JR NZ,CRC_1
		DEC BC
		LD A,B
		OR C
		JR NZ,CRC_0
		RET

;---------------------------------
;ÉêìáàãäÄ îÄâãÄ èé ìäÄáÄççéåì èìíà

BUF_512		EQU #5000		;#200 ÅìîÖê ëÖäíéêÄ
TDIRCLS		EQU BUF_512+#0200	;#400 ÅìîÖê äãÄëíÖêéÇ ROOT ÑàêÖäíéêàà
CAL_FAT		EQU TDIRCLS+#0400	;1 äÄãàÅê FAT
BYTSSEC		EQU CAL_FAT+1		;1 äéãàóÖëíÇé ëÖäíéêéÇ Ç äãÄëíÖêÖ
ROOTCLS		EQU BYTSSEC+1		;4 äãÄëíÖê çÄóÄãÄ ROOT ÑàêÖäíéêàà
ROOTSEC		EQU ROOTCLS+4		;2 êÄáåÖê Ç ëÖäíéêÄï ROOT ÑàêÖäíéêàà
SEC_FAT		EQU ROOTSEC+2		;4 äéãàóÖëíÇé ëÖäíéêéÇ éÑçéâ FAT
RSVDSEC		EQU SEC_FAT+4		;2 êÄáåÖê êÖáÖêÇçéâ éÅãÄëíà
STARTRZ		EQU RSVDSEC+2		;4 çÄóÄãé ÑàëäÄ/êÄáÑÖãÄ
FRSTDAT		EQU STARTRZ+4		;4 ÄÑêÖë èÖêÇéÉé ëÖäíéêÄ ÑÄççõï éí BPB
SEC_DSC		EQU FRSTDAT+4		;4 äéãàóÖëíÇé ëÖäíéêéÇ çÄ ÑàëäÖ/êÄáÑÖãÖ
CLS_DSC		EQU SEC_DSC+4		;4 äéãàóÖëíÇé äãÄëíÖêéÇ çÄ ÑàëäÖ/êÄáÑÖãÖ
FATSTR		EQU CLS_DSC+4		;4 çÄóÄãé èÖêÇéâ FAT íÄÅãàñõ
FB_EXT		EQU FATSTR+4		;B ÅìîÖê 8.3 Ñãü èéàëäÄ àåÖçà
ADRPATH		EQU FB_EXT+#0B		;2 ÄÑêÖë íÖäëíÄ èìíà îÄâãÄ
STATUS		EQU ADRPATH+2		;1 ëíÄíìë èéëãÖ ÇõáéÇÄ LOAD_SD
OLD_SP		EQU STATUS+1		;2 ëíÖä Ñãü ÇõïéÑÄ

;SD äÄêíÄ çÖ çÄâÑÖçÄ
ZAW003		LD A,#EE
WR_STAT		LD SP,(OLD_SP)
		LD (STATUS),A
		RET

;áÄÉêìáäÄ îÄâãÄ
;çÄ ÇïéÑÖ:A-ëíêÄçàñÄ çÄóÄãÄ áÄÉêìáäà
;HL-ÄÑêÖë íÖäëíéÇéâ ëíêéäà
;èìíà ä îÄâãì ÇåÖëíÖ ë àåÖçÖå à êÄëòàêÖçàÖå îÄâãÄ. èìíú èéãçéëíúû éí ROOT 
;áÄÉêìÜÄÖíëü îÄâã èé êÄáåÖêì ÑéèéãçÖçéåì Ñé èéãçõï ëÖäíéêéÇ (ëÖäíéê 512 ÅÄâí)
;èêàåÖê: êÄáåÖê îÄâãÄ =#80-èéëãÖ ÑéèéãçÖçàü ÅìÑÖí áÄÉêìÜÖç 1 ëÖäíéê
;=#401-ÅìÑÖí áÄÉêìÜÖçé 3 ëÖäíéêÄ
;çÄ ÇõïéÑÖ: A=
;#00-îÄâã áÄÉêìÜÖç
;#AA-îÄâã çÖ çÄâÑÖç
;#DD-FAT çÖ éÅçÄêìÜÖç
;#EE-SD äÄêíÄ çÖ éÅçÄêìÜÖçÄ
LOAD_SD		LD LY,A			;ëéïêÄçàãà çéåÖê ëíêÄçàñõ äìÑÄ Éêìáàíú
		LD (ADRPATH),HL		;ëéïêÄçàãà ÄÑêÖë ëíêéäà èìíà
		LD (OLD_SP),SP		;ëéïêÄçàãà ëíÖä
		LD A,1
		OUT (GSCFG0),A		;éíäãûóàãà èáì, ÇëÖ ëíêÄçàñÄ éáì
		LD A,%10011011
		OUT (SCTRL),A		;ëäéçîàÉìêàãà çÉë ë CS=1 Ñãü SD äÄêíõ		
		LD B,#10
		LD A,#FF
		OUT (SD_SEND),A		;èàòÖå #80 ÅÄâí #FF Ç èéêí äÄêíéóäà
		DJNZ $-4
		XOR A			;256 èéèõíéä çÄâíà SD äÄêíì
		EXA
		LD A,1
		OUT (SCTRL),A		;ÇõÅêÄãà SD äÄêíì CS=0

ZAW001		LD HL,CMD00
		CALL OUTCOM		;èÖêÖÇéÑàå äÄêíéóäì Ç êÖÜàå SPI äéåÄçÑéâ 0
		CALL IN_OOUT		;ÜÑÖå éíÇÖíÄ
		EXA
		DEC A
		JR Z,ZAW003		;ÜÑÖå èé ëóÖèíóàäì 256 êÄá
		EXA
		DEC A
		JR NZ,ZAW001		;ÜÑÖå èéäÄ äÄêíÄ éíÇÖíàí ÅÄâíéå 1
		LD BC,SD_RSTR
		LD HL,CMD08
		CALL OUTCOM		;éèêÖÑÖãüÖå ëèÖñàîàäÄñàû äÄêíõ
		CALL IN_OOUT		;Ç "A" éíÇÖí äÄêíõ R1
		IN H,(C)
		NOP
		IN H,(C)	
		NOP
		IN H,(C)
		NOP
		IN H,(C)		;èêéóàíÄãà éëíÄãúçõÖ ÅÄâíõ Ç çàäìÑÄ
		BIT 2,A			;Öëãà éòàÅäÄ, íé
		LD HL,0			;äÄêíÄ ëèÖñàîàäÄñàà 1.0
		JR NZ,ZAW006		;àçÄóÖ 
		LD H,#40		;äÄêíÄ ëèÖñàîàäÄñàà 2.0
ZAW006		LD A,CMD_55
		CALL OUT_COM		;áÄèìëäÄÖå ÇçìíêÖççûû àçàñàÄãàáÄñàû äÄêíõ
		CALL IN_OOUT
		LD BC,SD_SEND
		LD A,ACMD_41
		OUT (C),A
		LD A,H
		OUT (C),A
		XOR A
		OUT (C),A
		NOP
		OUT (C),A
		NOP
		OUT (C),A
		DEC A
		OUT (C),A
		CALL IN_OOUT
		AND A
		JR NZ,ZAW006		;ÜÑÖå èéäÄ äÄêíõ èÖêÖâÑÖí Ç êÖÜàå ÉéíéÇçéëíà
ZAW004		LD A,CMD_59
		CALL OUT_COM		;èêàçìÑàíÖãúçé éíäãûóÄÖå CRC16
		CALL IN_OOUT
		AND A
		JR NZ,ZAW004
ZAW005		LD HL,CMD16
		CALL OUTCOM		;èêàçìÑàíÖãúçõâ êÄáåÖê ëÖäíéêÄ 512 ÅÄâí
		CALL IN_OOUT
		AND A
		JR NZ,ZAW005

;àçàñàÄãàáÄñàü èÖêÖåÖççõï FAT
WC_FAT		LD DE,0
		LD B,D
		LD C,E
		CALL LOADLST		;óàíÄÖå ëÖäíéê 0 äÄêíéóäà
		PUSH HL
		POP IX
		LD DE,#01BE
		ADD HL,DE		;èÖêÖïéÑàå çÄ ëåÖôÖçàÖ Ñãüó èêéÇÖêéä
		LD A,(HL)		;èêéÇÖêüå óíéÅõ Åõã 0, äÄêíéóäà çÖ åéÉìí Åõíú áÄÉêìáéóçõåà
		AND A
		JR NZ,RDFAT05		;Öëãà çÖ 0, èêéÇÖêàíú ÑêìÉéÖ
		LD DE,4
		ADD HL,DE		;èÖêÖïéÑàå ä èêéÇÖêäÖ íàèÄ êÄáÑÖãÄ
		LD A,(HL)
		LD B,0
		CP 1			;FAT12?
		JR Z,RDFAT06
		LD B,2
		CP #0B			;FAT32?
		JR Z,RDFAT06
		CP #0C			;FAT32?
		JR Z,RDFAT06
		LD B,1
		CP 6			;FAT16?
		JR Z,RDFAT06
		CP #0E			;FAT16?
		JR NZ,RDFAT05		
RDFAT06		LD A,B			;ÅÖêÖå àá "B" íàè êÄáÑÖãÄ
		LD (CAL_FAT),A		;ëéïêÄçàãà
		ADD HL,DE
		CALL LOADZP		;ÅÖêÖå çéåÖê ëÖäíéêÄ çÄóÄãÄ éëçéÇçéÉé êÄáÑÖãÄ
		JR RDFAT00		;èÖêÖïéÑàå ä àçàñàÄãàáÄñàà èÖêÖåÖççõï Ñãü êÄÅéíõ ë îÄíéå

;MBR çÖ éÅçÄêìÜÖç, èêéÇÖêüÖå ëÖäíéê 0 äÄêíõ äÄä éèàëÄíÖãú
RDFAT05		LD C,(IX+#0D)		;C=äéãàóÖëíÇé ëÖäíéêéÇ Ç äãÄëíÖêÖ
		XOR A
		LD E,A
		LD B,8
		RR C
		ADC A,0
		DJNZ $-4		;äéãàóÖëíÇé ëÖäíéêéÇ Ç äãÄëíÖêÖ ÑéãÜçé Åõíú ëíÖèÖçúû 2
		DEC A
		JR NZ,$+3		;èêéÇÖêàãà äéãàóÖëíÇé Åàí
		INC E			;+1, Öëíú íÄäéÖ
		LD A,(IX+#0E)
		OR (IX+#0F)
		JR Z,$+3		;äéãàóÖëíÇé áÄêÖáÖêÇàêéÇÄççõï ëÖäíéêéÇ ÑéãÜçé Åõíú >0
		INC E			;+1, Öëíú íÄäéÖ
		LD A,(IX+#13)
		OR (IX+#14)
		JR NZ,$+3		;äéãàóÖëíÇé ëÖäíéêéÇ çÄ êÄáÑÖãÖ Ñãü îÄí16?
		INC E
		LD A,(IX+#20)
		OR (IX+#21)
		OR (IX+#22)
		OR (IX+#23)
		JR NZ,$+3		;äéãàóÖëíÇé ëÖäíéêéÇ çÄ êÄáÑÖãÖ Ñãü îÄí32?
		INC E			;éÑçé àá çàï ÑéãÜçé Åõíú =0, ÑêìÉéÖ >0
		LD A,(IX+#15)
		AND #F0
		CP #F0
		JR NZ,$+3		;ëíÄêòàÖ Åàíõ ÑéãÜçõ Åõíú Ç 1
		INC E
		LD A,E
		CP 4			;ìëãéÇàü ëéÇèÄãà?
		LD A,#DD		;FAT çÖ çÄâÑÖç
		JP NZ,WR_STAT
		LD A,#FF
		LD (CAL_FAT),A		;íàè îÄí èéäÄ çÖ éèêÖÑÖãÖç
		LD DE,0
		LD B,D
		LD C,E

RDFAT00		LD (STARTRZ),DE
		LD (STARTRZ+2),BC	;èéãéÜàãà çéåÖê ëíÄêíéÇéÉé ëÖäíéêÄ êÄáÑÖãÄ
		CALL LOADLST		;áÄÉêìáàãà ÖÉé
		LD HL,0
		LD DE,(BUF_512+#16)	;BPB_FATSZ16
		LD A,D
		OR E
		JR NZ,RDFAT01		;Öëãà çÖ FAT12/16 (BPB_FATSZ16=0)
		LD DE,(BUF_512+#24)
		LD HL,(BUF_512+#26)	;BPB_FATSZ32
					;íé ÅÖêÖå àá ëåÖôÖçàü +36
RDFAT01		LD (SEC_FAT+2),HL
		LD (SEC_FAT),DE		;óàëãé ëÖäíéêéÇ çÄ FAT-íÄÅãàñì
		LD HL,0
		LD DE,(BUF_512+#13)	;BPB_TOTSEC16
		LD A,D
		OR E
		JR NZ,RDFAT02		;Öëãà çÖ FAT12/16 (BPB_TOTSEC16=0)
		LD DE,(BUF_512+#20)
		LD HL,(BUF_512+#22)	;BPB_TOTSEC32
					;íé ÅÖêÖå àá ëåÖôÖçàü +32
RDFAT02		LD (SEC_DSC+2),HL
		LD (SEC_DSC),DE		;ä-Çé ëÖäíéêéÇ çÄ ÑàëäÖ/êÄáÑÖãÖ

;ÇõóàëãüÖå ROOTDIRSECTORS
		LD BC,(BUF_512+#0B)	;BPB_BYTSPERSEC
		LD DE,(BUF_512+#11)	;BPB_ROOTENTCNT
		LD HL,0
		LD A,D
		OR E
		JR Z,RDFAT03
		LD B,H
		LD C,L
		LD A,#10
		CALL BCDE_A
		EX DE,HL

;ùíé êÖÄãàáéÇÄçÄ îéêåìãÄ
;ROOTDIRSECTORS=((BPB_ROOTENTCNT*32)+(BPB_BYTSPERSEC-1))/BPB_BYTSPERSEC
;Ç HL=ROOTDIRSECTORS. Öëãà FAT32, íé HL=0 ÇëÖÉÑÄ

RDFAT03		PUSH HL			;ROOTDIRSECTORS
		LD (ROOTSEC),HL
		LD A,(BUF_512+#10)
		LD DE,(SEC_FAT)
		LD HL,(SEC_FAT+2)
		DEC A
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		DEC A
		JR NZ,$-6
		POP BC			;èéãçõâ êÄáåÖê FAT-éÅãÄëíà Ç ëÖäíéêÄï
		CALL HLDEPBC		;èêàÅÄÇàãà ROOTDIRSECTORS
		LD BC,(BUF_512+#0E)	;BPB_RSVDSECCNT
		LD (RSVDSEC),BC
		CALL HLDEPBC		;èêàÅÄÇàãà BPB_RESVDSECCNT
		LD (FRSTDAT),DE
		LD (FRSTDAT+2),HL	;èéãéÜàãà çéåÖê èÖêÇéÉé ëÖäíéêÄ ÑÄççõï
		LD B,H
		LD C,L
		LD HL,SEC_DSC		;BCDE+32-Æ• óàëãé èé ÄÑêÖëì HL
		CALL BCDEHLM		;Çõóãà àá èéãçéÉé ä-ÇÄ ëÖäíéêéÇ êÄáÑÖãÄ
		LD A,(BUF_512+#0D)
		LD (BYTSSEC),A
		CALL BCDE_A		;êÄáÑÖãàãà çÄ ä-Çé ëÖäíéêéÇ Ç äãÄëíÖêÖ
		LD (CLS_DSC),DE
		LD (CLS_DSC+2),BC	;èéãéÜàãà äéã-Çé äãÄëíÖêéÇ çÄ êÄáÑÖãÖ

		LD A,(CAL_FAT)
		CP #FF
		JR NZ,RDFAT04
		LD HL,(CLS_DSC)		;Öëãà êÄáêîÑçéëíú îÄíÄ çÖ éèêÖÑÖãÖçÄ, éèêÖÑÖãüÖå
		LD DE,(CLS_DSC+2)	;Çáüãà äéãàóÖëíÇé äãÄëíÖêéÇ çÄ êÄáÑÖãÖ
		PUSH HL
		PUSH DE
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD B,H
		LD C,L
		CALL RASCHET		;ëóàíÄÖå, Öëãà èéãìóàãà 0, íé îÄí16
		LD A,1
		POP DE
		POP HL
		JR Z,RDFAT04
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD B,H
		LD C,L
		CALL RASCHET		;ëóàíÄÖå, Öëãà èéãìóàãà 0, íé îÄí32
		LD A,2
		JR Z,RDFAT04
		XOR A			;àçÄóÖ îÄí12

;Ñãü FAT12/16 ÇõóàëãüÖå ÄÑêÖë èÖêÇéÉé ëÖäíéêÄ ÑàêÖäíéêàà
;Ñãü FAT32 ÅÖêÖå èé ëåÖôÖåàû +44, çÄ ÇõïéÑÖ BCDE-ëÖäíéê ROOTDIR
RDFAT04		PUSH AF
		LD DE,(RSVDSEC)
		LD BC,0
		LD HL,STARTRZ
		CALL BCDEHLP
		LD (FATSTR),DE
		LD (FATSTR+2),BC	;Çõóàëãàãà à èéãéÜàãà çéåÖê ëÖäíéêÄ çÄóÄãÄ FAT-ÖÄÅãàñ
		POP AF
		LD (CAL_FAT),A		;ìíéóçàãà íàè îÄíÄ
		AND A
		LD DE,0
		LD B,D
		LD C,E
		JR Z,FSRROO2		;FAT12-NONE
		DEC A
		JR Z,FSRROO2		;FAT16
		LD DE,(BUF_512+#2C)
		LD BC,(BUF_512+#2E)	;FAT32
FSRROO2		LD (ROOTCLS),DE
		LD (ROOTCLS+2),BC	;èéãéÜàãà çéåÖê äãÄëíÖê ROOT ÑàêÖäíéêàà

		LD HL,(ADRPATH)		;ÇÖêçìãà ÄÑêÖë ëíêéäà èìíà Ñé îÄâãÄ
FINDFL1		PUSH BC
		PUSH DE			;ëéïêÄçàãà çéåÖê äãÄëíÖêÄ
		CALL FNDBUF		;êÄëèÄäéÇäÄ óÄëíà íÖäëíéÇéâ ëíêéäà Ñãü ëéáÑÄçàü åÄëäà èéàëäÄ
		POP DE
		POP BC			;ÇéëëíÄçéÇàãà çéåÖê äãÄëíÖêÄ
		PUSH HL			;ëéïêÄçàãà íÖäìôàâ ÄÑêÖë íÖäëíéÇéâ ëíêéäà

		LD HL,TDIRCLS		;ÄÑêÖë íÄÅãàñõ äãÄëíÖêéÇ íÖäìôÖâ ÑàêÖäíéêàà
		LD A,D
		OR E
		OR B
		OR C
		CALL SAVEZP		;ëéïêÄçàãà Ç íÄÅãàñì çéåÖê íÖäìôÖÉé äãÄëíÖêÄ
		JR Z,LASTCLS		;Öëãà çéåÖê äãÄëíÖêÄ 0, íé ùíé ROOT ÑàêÄ (Ñãü îÄí12/16)
NEXTCLS		PUSH HL
		CALL RDFATZP		;óàíÄÖå ëãÖÑìôàâ çéåÖê äãÄëíÖêÄ àá ñÖèéóäà ÑàêÖäíéêàà
		CALL LST_CLS		;èêéÇÖêüÖå çÄ äéçÖñ ñÖèéóäà
		POP HL
		JR Z,LASTCLS
		CALL SAVEZP		;Öëãà çÖèéëãÖÑçàâ ëéïêÄçüÖå Ç íÄÅãàñì
		JR NEXTCLS		;ëãÖÑìûôàâ çéåÖê äãÄëíÖêÄ

LASTCLS		LD BC,#FFFF
		CALL SAVEZP		;äãÄÑÖå åÄêäÖê äéçñÄ ñÖèéóäà

FINDFL		INC BC			;àôÖå èé áÄÑÄççéâ åÄëäÖ çÄóàçÄü ë 0
		CALL RDDIRSC		;Éêìáàå èé çéåÖêì éèàëÄíÖãü ëÖäíéê ÑàêÖäíéêàà
		LD A,C
		AND #0F			;Ç ëÖäíéêÖ åÄäëàåìå 16 éèàëÄíÖãÖâ
		LD E,A
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE		;èéãìóàãà ÄÑêÖë çìÜçéÉé éèàëÄíÖãü
		LD A,(HL)		;èêéÇÖêüÖå èÖêÇõâ ÅÄâí àåÖçà éèàëÄíÖãü
		AND A
		LD A,#AA		;Öëãà ÅÄâí =0, íé
		JP Z,WR_STAT		;èÖêÖïéÑ èé éòàÅäÖ = îÄâã çÖ çÄâÑÖç
		PUSH HL
		PUSH BC
		CALL COMPARE		;ëêÄÇçàÇÄÖå ë áÄÑÄççéâ åÄëäéâ
		POP BC
		POP DE
		PUSH DE
		POP IX			;ëéÑÖêÜàåéÖ IX=ÄÑêÖë éèàëÄíÖãü
		JR NZ,FINDFL		;çÖ ëéÇèÄÑÄÖí, èÖêÖïéÑàå ä ëãÖÑìûôÖåì éèàëÄíÖãû
		CALL RD_CLAS		;áÄÅàêÄÖå çéåÖê äãÄëíÖêÄ àá çÄâÑÖççéÉé éèàëÄíÖãü
		EX (SP),HL		;ÇéëëíÄçéÇàãà íÖäìôàâ ÄÑêÖë Ç ëíêéäÖ èìíà Ñé îÄâãÄ
		INC SP
		INC SP			;åÄëäàêéÇäÄ ÄÑêÖëÄ êÄáåÖêÄ Ç ÅÄâíÄï íÖäìôÖÉé îÄâãÄ
		LD A,(HL)
		AND A			;íÖäëíéÇÄü ëíêéäÄ äéçóàãÄëú?
		JR NZ,FINDFL1		;Öëãà çÖí, íé àôÖå ÑÄãúòÖ
		LD A,(IX+#0B)		;èêéÇÖêüÖå ùíé ÑàêÄ àãà îÄâã?
		AND #10
		LD A,#AA		;Öëãà ÑàêÄ, íé éòàÅäÄ
		JP NZ,WR_STAT		;íÖäëíéÇÄü ëíêéäÄ ÑéãÜçÄ ìäÄáõÇÄíú çÄ îÄâã
		DEC SP
		DEC SP
		POP HL			;ÇéëëíÄçéÇàãà Ç HL ÄÑêÖë éíäìÑÄ Çáüíú êÄáåÖê îÄâãÄ Ç ÅÄâíÄï
		PUSH BC
		PUSH DE
		CALL LOADZP		;áÄÅàêÄÖå êÄáåÖê îÄâãÄ (Ç ÅÄâíÄï)
		LD A,E
		AND A
		JR Z,$+3		;åãÄÑòàâ ÅÄâí êÄáåÖêÄ îÄâãÄ =0?
		INC D			;ìÇÖãàóàÇÄÖå êÄáåÖê çÄ 256 ÅÄâí ÅÖá ìóÖíÄ åãÄÑòÖÉé ÅÄâíÄ
		BIT 0,D			;èêéÇÖêüÖå óÖí/çÖóÖí
		JR Z,$+3		;Öëãà çÖóÖí, íé
		INC D			;ìÇÖãàóàÇÄÖå êÄáåÖê ÖôÖ çÄ 256 ÅÄâí
		CALL BCDE200		;ÑÖãàå çÄ 512 (çÄ êÄáåÖê ëÖäíéê)
		PUSH DE
		EXX
		POP HL
		EXX
		LD A,(BYTSSEC)		;Çáüãà êÄáåÖê äãÄëíÖêÄ Ç ëÖäíéêÄï
		LD HX,A			;ëéïêÄçàãà
		POP DE
		POP BC
		LD HL,#8000		;ÄÑêÖë áÄÉêìáäà

;HX-êÄáåÖê äãÄëíÖêÄ
;HL'-äéã-Çé ëÖäíéêéÇ îÄâãÄ
;BCDE-çéåÖê ëíÄêíéÇéÉé äãÄëíÖêÄ
;LY-ëíÄêíéÇÄü ëíêÄçàñÄ áÄÉêìáäà

		LD A,LY			;ÇéëëíÄçéÇàãà ëíêÄçàñì áÄÉêìáäà
		AND A
		JR NZ,CP_PAGE		;èêéÇÖêäÄ Ñãü áÄÉêìáäà Ç ëíêÄçàñì 0
		OUT (MPAG),A		;ÇäãûóÄÖå ëíêÄçàñì 0
		EXX
		LD A,L
		LD DE,#41
		SBC HL,DE
		JR C,$+4		;Öëãà îÄâã #41 à ÅéãÖÖ ëÖäíéêéÇ
		LD A,#40		;íé Éêìáàå íéãúäé #40 èÖêÇõï ëÖäíéêéÇ
		EXX
		JP LDMINI		;èÖêÖïéÑàå çÄ áÄÉêìáäì

;áÄÉêìáäÄ Ç ëíêÄçàñì 1 áÄèêÖôÖçÄ
CP_PAGE		DEC A
		LD A,#AA
		JP Z,WR_STAT
		LD A,LY

;ÇäãûóÄÖå ëíêÄçàñì áÄÉêìáäà
LDFILE0		OUT (MPAG),A		;ÇäãûóÄÖå áÄÑÄççìû ëíêÄçàñì Ñãü áÄÉêìáäà

;áÄÉêìáäÄ Ç ëíêÄçàñõ 02...7F
LD_FILE		EXX
		LD E,HX
		LD D,0			;DE=êÄáåÖê äãÄëíÖêÄ Ç ëÖäíéêÄï
		AND A
		SBC HL,DE		;ëÇÖêüÖå ë äéãàóÖëíÇéå ëÖäíéêéÇ Ñãü áÄÉêìáäà
		LD LX,HX		;LX=äéãàóÖëíÇé ëÖäíéêéÇ Ñãü áÄÉêìáäà
		EXX
		JR NC,LDFILE1
		EXX			;ëÖäíéêéÇ Ñãü áÄÉêìáäà åÖçúòÖ êÄáåÖêÄ äãÄëíÖêÄ
		ADD HL,DE	
		LD A,L
		LD LX,A			;LX=äéãàóÖëíÇé ëÖäíéêéÇ Ñãü áÄÉêìáäà
		EXX

LDFILE1		PUSH BC
		PUSH DE
		PUSH HL
		CALL REALSEC		;èÖêÖÇÖãà çéåÖê äãÄëíÖêÄ Ç çéåÖê êÖÄãúçéÉé ëÖäíéêÄ
		LD A,LX
		CP #41			;äéãàóÖëíÇé ëÖäíéêéÇ Ñãü áÄÉêìáäà ÅéãúòÖ #40?
		JR C,$+4
		LD A,#40		;ÅìÑÖå Éêìáàíú #40 ëÖäíéêéÇ
		POP HL
		LD HY,A
		CALL RDMULTI		;áÄÉêìÜÄÖå ëÖäíéêÄ
		LD A,HX
		AND #80
		JR Z,LDFILE2
		LD A,LX
		SUB HY
		JR Z,LDFILE4
		JR C,LDFILE4
		LD HL,#40
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		LD L,A
		INC LY
		LD A,LY
		CP #40
		JR C,LDFILE3
LDFILE4		INC SP
		INC SP
		INC SP
		INC SP
		JR LDEFILE

LDFILE3		OUT (MPAG),A		;ëãÖÑìûôÄü ëíêÄçàñÄ
		LD A,L
		LD HL,#8000
		CALL RDMULTI
LDFILE2		POP DE
		POP BC
		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR Z,LDEFILE
		LD A,LX
		CP HX
		JR C,LDEFILE
		LD A,H
		AND A
		JR NZ,LD_FILE
		LD HL,#8000
		INC LY
		LD A,LY
		CP #40
		JR C,LDFILE0
LDEFILE		XOR A
		JP WR_STAT

LDMINI		EXX
		LD L,A
		LD A,HX
		LD H,A
		CP L
		JR C,$+3
		LD A,L
		EXX
		PUSH BC
		PUSH DE
		PUSH AF
		PUSH HL
		CALL REALSEC
		POP HL
		POP AF
		CALL RDMULTI
		POP DE
		POP BC
		LD A,H
		AND A
		RET Z
		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR Z,LDEFILE
		EXX
		LD A,L
		SUB H
		EXX
		JR NC,LDMINI
		JR LDEFILE

SAVEZP		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		LD (HL),C
		INC HL
		LD (HL),B
		INC HL
		RET

LOADZP		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		INC HL
		RET

;óíÖçàÖ ëÖäíéêÄ DIR èé çéåÖêì BC
RDDIRSC		PUSH BC
		LD D,B
		LD E,C
		LD BC,0
		LD A,#10
		CALL BCDE_A
		LD A,E
		PUSH AF
		LD A,(BYTSSEC)
		PUSH AF
		CALL BCDE_A
		LD HL,TDIRCLS
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		CALL LOADZP
		CALL REALSEC
		POP AF
		DEC A
		LD L,A
		POP AF
		AND L
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		CALL LOADLST
		POP BC
		RET

LST_CLS		LD A,(CAL_FAT)
		AND A
		JR NZ,LST_CL1
		LD HL,#0FFF
		SBC HL,DE
		RET

LST_CL1		DEC A
		JR NZ,LST_CL2
LST_CL3		LD HL,#FFFF
		SBC HL,DE
		RET

LST_CL2		LD HL,#0FFF
		SBC HL,BC
		RET NZ
		JR LST_CL3

RDFATZP		LD A,(CAL_FAT)
		AND A
		JR Z,RDFATS0
		DEC A
		JR Z,RDFATS1
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		LD HL,0
		ADC HL,BC
		ADC HL,BC
		LD A,E
		LD E,D
		LD D,L
		LD C,H
		LD B,0
		CALL RDFATS2
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		RET

RDFATS1		LD BC,0
		LD A,E
		LD E,D
		LD D,C
RDFATS2		PUSH AF
		PUSH BC
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		POP AF
		LD E,A
		LD D,0
		ADD HL,DE
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		RET

RDFATS0		LD H,D
		LD L,E
		ADD HL,HL
		ADD HL,DE
		SRL H
		RR L
		LD A,E
		LD E,H
		LD D,0
		LD B,D
		LD C,D
		SRL E
		PUSH AF
		PUSH HL
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		LD A,B
		AND 1
		LD B,A
		ADD HL,BC
		LD B,(HL)
		INC HL
		LD A,H
		CP LOW BUF_512+2
		JR NZ,RDFATS4
		PUSH BC
		LD BC,0
		INC DE
		CALL LOADLST
		POP BC
RDFATS4		POP AF
		LD D,(HL)
		LD E,B
		LD BC,0
		RRA
		JR NC,RDFATS3
		SRL D
		RR E
		SRL D
		RR E
		SRL D
		RR E
		SRL D
		RR E
RDFATS3		LD A,D
		AND #0F
		LD D,A
		RET

;ÇõóàëãÖçàÖ êÖÄãúçéÉé ëÖäíéêÄ
;çÄ ÇïéÑÖ BCDE=çéåÖê FAT
;çÄ ÇõïéÑÖ BCDE=ÄÑêÖë ëÖäíéêÄ
REALSEC		LD A,B
		OR C
		OR D
		OR E
		JR NZ,REALSE1
		LD HL,SEC_FAT
		LD DE,(FATSTR)
		LD BC,(FATSTR+2)
		PUSH HL
		CALL BCDEHLP
		POP HL
		JP BCDEHLP

REALSE1		LD HL,#FFFE
		EX DE,HL
		ADD HL,DE
		EX DE,HL
		INC HL
		ADC HL,BC		;çéåÖê äãÄëíÖêÄ-2
		LD A,(BYTSSEC)
		JR REALSE2

REALSE3		SLA E
		RL D
		RL L
		RL H
REALSE2		RRCA
		JR NC,REALSE3		;ìåçéÜàãà çÄ êÄáåÖê äãÄëíÖêÄ
		LD B,H
		LD C,L
		LD HL,STARTRZ
		CALL BCDEHLP		;èêàÅÄÇàãà ëåÖôÖçàÖ éí çÄóÄãÄ ÑàëäÄ
		LD HL,FRSTDAT
		JP BCDEHLP		;èêàÅÄÇàãà ëåÖôÖçàÖ éí çÄóÄãÄ êÄáÑÖãÄ

BCDE200		LD E,D
		LD D,C
		LD C,B
		LD B,0
		LD A,2
		JR BCDE_A

;BCDE>>A=BCDE
BCDE_A1		SRL B
		RR C
		RR D
		RR E
BCDE_A		RRCA
		JR NC,BCDE_A1
		RET

;(ADR)-BCDE=BCDE
BCDEHLM		LD A,(HL)
		INC HL
		SUB E
		LD E,A
		LD A,(HL)
		INC HL
		SBC A,D
		LD D,A
		LD A,(HL)
		INC HL
		SBC A,C
		LD C,A
		LD A,(HL)
		SBC A,B
		LD B,A
		RET

;(ADR)+BCDE=BCDE
BCDEHLP		LD A,(HL)
		INC HL
		ADD A,E
		LD E,A
		LD A,(HL)
		INC HL
		ADC A,D
		LD D,A
		LD A,(HL)
		INC HL
		ADC A,C
		LD C,A
		LD A,(HL)
		ADC A,B
		LD B,A
		RET

;HLDE+BC=HLDE
HLDEPBC		EX DE,HL
		ADD HL,BC
		EX DE,HL
		LD BC,0
		ADC HL,BC
		RET

;éèêÖÑÖãüíéê êÄáêüÑçéëíà FAT
RASCHET		CALL BCDE200
		LD HL,SEC_FAT
		CALL BCDEHLM
		LD A,E
		AND #F0
		OR D
		OR C
		OR B
		RET

;ÉêìáàãäÄ éÑçéÉé ëÖäíéêÄ
LOADLST		LD HL,BUF_512
		LD A,1
		PUSH HL
		CALL RDMULTI
		POP HL
		RET

OUTCOM		PUSH BC
		LD BC,#0600+SD_SEND
		OTIR
		POP BC
		RET

OUT_COM		PUSH BC
		LD BC,SD_SEND
		OUT (C),A
		XOR A
		OUT (C),A
		NOP
		OUT (C),A
		NOP
		OUT (C),A
		NOP
		OUT (C),A
		DEC A
		OUT (C),A
		POP BC
		RET

SECM200		PUSH HL
		PUSH BC
		LD A,CMD_58
		CALL OUT_COM
		CALL IN_OOUT
		LD BC,SD_RSTR
		IN H,(C)
		NOP
		IN A,(C)
		NOP
		IN A,(C)
		NOP
		IN A,(C)
		BIT 6,H
		POP HL
		JR NZ,SECN200
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD H,L
		LD L,D
		LD D,E
		LD E,0
SECN200		LD A,CMD_18
		LD C,SD_SEND
		OUT (C),A
		NOP
		OUT (C),H
		NOP
		OUT (C),L
		NOP
		OUT (C),D
		NOP
		OUT (C),E
		LD A,#FF
		OUT (C),A
		POP HL
		RET

IN_OOUT		EXX
		LD DE,#20FF
IN_WAIT		IN A,(SD_RSTR)
		CP E
		JR NZ,IN_EXIT
IN_NEXT		DEC D
		JR NZ,IN_WAIT
IN_EXIT		EXX
		RET

CMD00		DB #40,#00,#00,#00,#00,#95	;GO_IDLE_STATE
CMD08		DB #48,#00,#00,#01,#AA,#87	;SEND_IF_COND
CMD16		DB #50,#00,#00,#02,#00,#FF	;SET_BLOCKEN

RDMULTI		EXA
		CALL SECM200
		EXA
		LD BC,SD_RSTR
RDMULT1		EXA
		CALL IN_OOUT
		CP #FE
		JR NZ,$-5
		INIR
		NOP
		INIR
		NOP
		IN A,(C)
		NOP
		IN A,(C)
		EXA
		DEC A
		JR NZ,RDMULT1
		LD A,CMD_12
		CALL OUT_COM
		CALL IN_OOUT
		INC A
		JR NZ,$-4
		RET

;ÇõÅéêäÄ çéåÖêÄ äãÄëíÖêÄ àá îÄâãéÇéÉé éèàëÄíÖãü
RD_CLAS		EX DE,HL
		LD DE,#14
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		RET

COMPARE		LD DE,FB_EXT
		LD B,#0B
		LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		DJNZ $-5
		RET

;êÄëèÄäéÇôàä èìíà ä îÄâãì
FNDBUF		LD BC,#0802
		LD DE,FB_EXT
FNDBUF4		LD A,(HL)
		INC HL
		CP #2E
		JR Z,FNDBUF2
		CP #5C
		JR Z,FNDBUF5
		LD (DE),A
		INC DE
		DJNZ FNDBUF4
		LD A,(HL)
		AND A
		RET Z
		INC HL
		JR FNDBUF3

FNDBUF5		LD A,C
		AND A
		RET Z
FNDBUF2		LD A,B
		AND A
		JR Z,FNDBUF3
		LD A,#20
		LD (DE),A
		INC DE
		DJNZ $-2
FNDBUF3		LD B,3
		DEC C
		DEC HL
		LD A,(HL)
		CP #5C
		JR Z,FNDBUF4
		INC HL
		JR FNDBUF4

RAMCEND
		ENT